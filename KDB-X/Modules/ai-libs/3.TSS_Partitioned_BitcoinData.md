# Effective TSS on a Partitioned Database

> **Note**: This tutorial is part of a beta release of software that is not yet publicly available. Please contact KX for access.

TSS (Temporal Similarity Search) is a technique used to find patterns in time series data by comparing a query pattern to a longer series. It’s useful when you want to identify where a particular shape or trend (like a spike, dip, or repeated structure) occurs in your data — such as finding similar price movements, temperature trends, or signal patterns.

This tutorial walks through the process of creating a partitioned database filled with BTC data. It then demonstrates how to conduct TSS efficiently across partitions, accounting for matches that may occur over across partition borders.

## 1. Prerequisites

1. Requires KDB-X to be installed, you can follow the installation guide at https://kx.com/products.
2. Ensure you have the necessary dataset:
   1. Download the Bitcoin sample dataset from Kaggle: [Cryptocurrency Timeseries 2020](https://www.kaggle.com/api/v1/datasets/download/roopahegde/cryptocurrency-timeseries-2020)
   2. Extract `archive.zip` into the same directory where you are running this from. Three files will be returned, but we're only going to be using `gemini_BTCUSD_2020_1min.csv`, which should be available in this directory.

## 2. Loading and Preparing the Data

Launch a q session and load the ai-libs initialization script:
```q
system"l ",getenv[`HOME],"/.kx/ai-libs/init.q"
```

Load the datasets into a table in memory, first we read in three 1-minute resolution crypto datasets for BTC, ETH, and LTC:

```q
tab:(" *SFFFFF";enlist ",") 0: `$":gemini_BTCUSD_2020_1min.csv";
tab,:(" *SFFFFF";enlist ",") 0: `$":gemini_ETHUSD_2020_1min.csv";
tab,:(" *SFFFFF";enlist ",") 0: `$":gemini_LTCUSD_2020_1min.csv";
```
Next, we parse and clean the time column the reorder the columns and sort the table ascending by time:

```q
time:sum@/:(("D";"N")$'/:" " vs/: tab`Date);
tab:`time xcols update time:time from delete Date from tab;
tab:`time xasc `time`sym`open`high`low`close`volume xcol tab;
```

Then save the data to disk partitioned on date:

```q
dts:asc exec distinct `date$time from tab;
{[dt;t]
    (hsym `$"cryptodb/",string[dt],"/trade/") set .Q.en[`:cryptodb] `time xasc select from t where dt=`date$time;
    }[;tab] each dts;
.Q.chk[`:cryptodb];
delete tab from `.;
.Q.gc[];
```

In the above we:
- Partitions your full timeseries dataset by date
- Saves each partition as a subdirectory under cryptodb/, e.g., cryptodb/2020.01.01/trade/
- Validates the result and frees up memory

This is a common pattern for managing large timeseries data efficiently, enabling fast, date-based querying and I/O performance.

Next, we load the on-disk data into our process without changing directory using:

```q
.Q.lo[`:cryptodb;0;0];
```

And to get an idea of the type of data we're working with we inspect the first record:

```q
first select from trade where date=first date, i=0
```
    date  | 2020.01.01
    time  | 2020.01.01D00:00:00.000000000
    sym   | `sym$`BTCUSD
    open  | 7165.9
    high  | 7170.79
    low   | 7163.3
    close | 7163.3
    volume| 0.00793095

The first record from the Kaggle: Cryptocurrency Timeseries 2020 dataset shows a 1-minute snapshot of BTC/USD trading on January 1st, 2020, including the timestamp, price range (open, high, low, close), and traded volume.

Now that we understand the structure of the data, we can move on to efficiently querying using TSS.

## 3. Performing TSS Searches

Now, we create a [random walk](#https://en.wikipedia.org/wiki/Random_walk) float vector which we will use as our pattern we wish to find matches of. We choose a pattern length of 64 arbitrarily here, feel free to experiment with your own query vectors.

```q
q:10*abs sums neg[0.5]+64?1f;
k:10;
```
In the above:
- `64?1f` generates 64 random float numbers between 0 and 1 
- `neg[0.5]` returns -0.5
- `sums` computes the cumulative sum, `abs` tables the absolute value of these and finally we multiply by `10*`

The result is a random pattern that fluctuates around zero, mimicking price fluctuations that we may want to detect in the time-series data.

There are two different use cases on partitioned databases:
1. **Patterns without Overlap** 

    For non-continuous data like NSYE market data, you only care about patterns within a single partition (e.g., within a single day). Here, you avoid searching across partition boundaries and focus on data within each partition.
2.  **Patterns with Overlap** 

    For time-series data like BTCUSD, data is partitioned by time (e.g., by day). You may want to find patterns that span across multiple partitions (e.g., a pattern from 2021-01-01 continuing into 2021-01-02). In this case, searches need to account for the overlap between partitions.

We show below a memory-efficient and parralelisable approach for partitioned databases, which can be applied to filtered searches as well.

### Example Query: Partition Optimised

This solution has two steps:

#### 1. Without Overlap

First we do a tss search by date using `.ai.tss.tss`, and then the relevant record is joined back to the starting point of the match using the IDs returned from the search. Here's the core query:

```q
t:select {a:.ai.tss.tss[x;q;k;`ignoreErrors`returnMatches!11b];a@\:iasc a[1]} close by date from trade;
```
Then, the result is processed to match relevant data, like so:
```q
res:select from trade where {[x;y;z] a:x[z;`close;1]; $[all null a;y#0b;@[y#0b;a;:;1b]]}[t;count i;first date];
```
The data is then flattened using `ungroup`, two new columns `dist` and `match` added populated with the distances and the matching values from the search. Finally the data is filtered to only retrieves the rows with the smallest `dist values`, which are considered the best matches.
```q
d:(0!t)`close;
res:res,' ungroup ([] dist:d[;0]; match:d[;2]);
res:`dist xasc select from res where i in k#iasc dist;
```
    date       time                          sym    open    high     low      close    volume   dist     match           ..
    ---------------------------------------------------------------------------------------------------------------------..
    2021.02.28 2021.02.28D00:01:00.000000000 ETHUSD 1451.26 1452.33  1444.15  1446     254.2706 10.48515 1446     169.79 ..
    2020.10.25 2020.10.25D00:00:00.000000000 ETHUSD 412.68  412.68   412.45   412.45   0.050261 10.51591 412.45   59.21  ..
    2021.02.03 2021.02.03D00:02:00.000000000 ETHUSD 1513.86 1520     1513.86  1518.87  23.50119 10.54675 1518.87  142.59 ..
    2021.04.11 2021.04.11D00:00:00.000000000 LTCUSD 255.86  257      255.86   257      326.6297 10.57468 257      59866.4..
    2021.01.24 2021.01.24D00:00:00.000000000 ETHUSD 1234.55 1234.58  1233.96  1233.97  11.96767 10.59025 1233.97  137.78 ..
    2020.12.13 2020.12.13D00:00:00.000000000 ETHUSD 568.07  568.07   568.07   568.07   0        10.60027 568.07   76.69  ..
    2021.02.21 2021.02.21D00:00:00.000000000 BTCUSD 56007.4 56046.79 55948.33 56006.17 1.74707  10.6025  56006.17 1917.08..
    2021.03.21 2021.03.21D00:02:00.000000000 ETHUSD 1796.12 1796.12  1788.25  1788.25  110.3599 10.6088  1788.25  198.65 ..
    2020.03.13 2020.03.13D02:23:00.000000000 BTCUSD 4098.96 4242.41  4080     4242.25  24.82005 10.61182 4242.25  93.33  ..
    2020.03.13 2020.03.13D02:22:00.000000000 BTCUSD 4077    4129.92  4077     4098.96  86.26804 10.61225 4098.96  92.2   ..


#### 2. With Overlap

Now we search across the overlap of dates:

```q
ovl:(0N;2*count[q])#count[q]_select from trade where (i in count[q]#i) | (i in neg[count[q]]#i);
ovltss:.ai.tss.tss[;q;k;`ignoreErrors`returnMatches!11b] each ovl[;`close];
```
In the above:
- `ovl` handles data that might span across the partition boundaries by focusing on the indices that exist in both partitions. The code builds an overlap list  and ensures that only the overlapping data from the partitions is used in the subsequent time series search.
- `ovltss` then performs `tss` for each of the overlapping partitions, ensuring that the relevant matching records (in this case, the close prices) are identified, even if they span across different partitions.

And finally we consolidate the two searches starting by filtering the `ovltss` results to keep only those where the values are less than the maximum value of the `dist` column (`maxTopK`). Then we consolidate the better matches from the previous step with the overlap data (`ovl`).

```q
maxTopK:max res`dist;
better:where@'ovltss[;0]<maxTopK;
betterOverlap:raze ovl@'ovltss[;1]@'better;
```
Next we extract the match data and the distance data and consolidate them into two separate lists, `match` and `dist`. Then we create a new table called `betterOverlapFull`, which combines the `betterOverlap` data with the `dist` and `match` data into a single table.

```q
match:raze ovltss[;2]@'better;
dist:raze ovltss[;0]@'better;
betterOverlapFull:betterOverlap,'([] dist:dist; match:match);
```
This process above is designed to refine the results, ensuring that only the best matches (those with lower distances) are kept, and all relevant information is combined into one final table, which is then sorted for the final output `res` that contains the top k closest matches sorted by distance:

```q
res:k#`dist xasc res,betterOverlapFull;
```
    date       time                          sym    open    high    low     close   volume   dist     match              ..
    ---------------------------------------------------------------------------------------------------------------------..
    2021.02.06 2021.02.06D23:49:00.000000000 ETHUSD 1682    1682    1679.91 1680    122.3035 9.602378 1680    155.87  393..
    2021.02.27 2021.02.27D23:49:00.000000000 ETHUSD 1462.66 1463.95 1461.38 1463.1  118.4681 9.674496 1463.1  172.32  463..
    2021.03.06 2021.03.06D23:50:00.000000000 ETHUSD 1659.32 1661.16 1658    1658.48 1.898126 9.82239  1658.48 182.92  488..
    2021.01.09 2021.01.09D23:47:00.000000000 ETHUSD 1292.94 1292.94 1290.67 1291.09 4.600949 9.839889 1291.09 177.94  404..
    2021.01.23 2021.01.23D23:48:00.000000000 ETHUSD 1229.2  1229.76 1227.86 1228.59 32.47425 9.854777 1228.59 137.45  320..
    2021.03.06 2021.03.06D23:55:00.000000000 ETHUSD 1659.13 1659.13 1656.56 1657.73 3.504332 9.99686  1657.73 182.92  489..
    2021.02.13 2021.02.13D23:49:00.000000000 ETHUSD 1820.24 1820.24 1816.85 1816.85 2.651055 10.08115 1816.85 228.26  471..
    2021.02.27 2021.02.27D23:54:00.000000000 ETHUSD 1465.42 1465.54 1462.78 1462.78 18.55336 10.10362 1462.78 172.66  462..
    2021.01.30 2021.01.30D23:49:00.000000000 LTCUSD 133.69  133.69  133.62  133.62  1.94868  10.10747 133.62  34306.3 137..
    2021.03.06 2021.03.06D23:48:00.000000000 ETHUSD 1663.14 1663.14 1658.36 1658.36 12.91813 10.1371  1658.36 182.76  489..


### Example Query: Filtered on Symbol

The previous example considered the table as a whole, which with our data doesn't give us too much of an indication of patterns, as we care about the patterns `by sym`.

Let's take a similar approach to the previous example, but this time we filter the data based on the symbol (`sym`). The goal is to identify patterns specifically for each cryptocurrency symbol, such as BTCUSD and ETHUSD.

We first select the close prices for the relevant symbols, iterating over the date-symbol pairs:

```q
t:select {a:.ai.tss.tss[x;q;k;`ignoreErrors`returnMatches!11b];a@\:iasc a[1]} close by date, sym from trade where sym in `BTCUSD`ETHUSD;
```

After the initial selection, we further process the data. This step processes each date-symbol pair and applies a transformation to extract the relevant information for each symbol and date. We then ungroup and process the data, obtaining a table that includes `dist` and `match` values for each entry.

```q
res:raze {select from trade where date = x[`date], sym = x[`sym], {[x;y] a:x[`close;1]; $[all null a;y#0b;@[y#0b;a;:;1b]]}[x;count i] } each 0!t;
d:(0!t)`close;
res:res,' ungroup ([] dist:d[;0]; match:d[;2]);
```

    date       time                          sym    open    high    low     close   volume     dist     match            ..
    ---------------------------------------------------------------------------------------------------------------------..
    2020.01.01 2020.01.01D00:44:00.000000000 BTCUSD 7154.63 7158.63 7154    7154    0.03557754 7.513186 7154    7152.54 7..
    2020.01.01 2020.01.01D05:39:00.000000000 BTCUSD 7199.95 7199.95 7199.95 7199.95 0.02461353 7.370183 7199.95 7199.95 7..
    2020.01.01 2020.01.01D09:36:00.000000000 BTCUSD 7174.09 7183.9  7174.09 7176.09 0.02011063 7.097173 7176.09 7176.09 7..
    2020.01.01 2020.01.01D10:38:00.000000000 BTCUSD 7181.31 7185.7  7180.52 7180.52 0.0134518  7.51535  7180.52 7180.55 7..
    2020.01.01 2020.01.01D10:39:00.000000000 BTCUSD 7180.52 7185.69 7180.52 7180.55 0.01364574 7.303182 7180.55 7180.55 7..
    2020.01.01 2020.01.01D10:40:00.000000000 BTCUSD 7180.55 7185.69 7180.55 7180.55 0.01344327 7.428542 7180.55 7180.55 7..
    2020.01.01 2020.01.01D10:41:00.000000000 BTCUSD 7180.55 7180.55 7180.55 7180.55 0          7.471221 7180.55 7180.55 7..
    2020.01.01 2020.01.01D14:51:00.000000000 BTCUSD 7203.12 7206.74 7200.93 7200.93 0.02722005 7.150772 7200.93 7200.98 7..
    2020.01.01 2020.01.01D14:52:00.000000000 BTCUSD 7200.93 7205.27 7200.93 7200.98 0.2272164  7.103358 7200.98 7200.98 7..
    2020.01.01 2020.01.01D16:28:00.000000000 BTCUSD 7196.3  7201.74 7195.84 7195.84 0.7949563  7.513712 7195.84 7202.73 7..
    2020.01.01 2020.01.01D05:26:00.000000000 ETHUSD 129.92  129.92  129.92  129.92  0.030634   6.976902 129.92  129.9   1..
    2020.01.01 2020.01.01D08:13:00.000000000 ETHUSD 129.86  129.87  129.82  129.87  0.030652   7.063389 129.87  129.8   1..
    2020.01.01 2020.01.01D10:37:00.000000000 ETHUSD 130.24  130.24  130.24  130.24  0          7.048045 130.24  130.24  1..
    2020.01.01 2020.01.01D10:38:00.000000000 ETHUSD 130.24  130.24  130.24  130.24  0          6.986929 130.24  130.36  1..
    2020.01.01 2020.01.01D10:39:00.000000000 ETHUSD 130.24  130.36  130.24  130.36  0.077239   6.871785 130.36  130.36  1..


Then we can filter down to the total k minimum matches that match our filter

```q
res:`dist xasc select from res where i in k#iasc dist;
show res;
```
    date       time                          sym    open     high     low      close    volume     dist     match        ..
    ---------------------------------------------------------------------------------------------------------------------..
    2021.02.09 2021.02.09D14:09:00.000000000 BTCUSD 46294.33 46329.31 46268.34 46306.86 0.3150739  5.56648  46306.86 4624..
    2020.01.15 2020.01.15D18:34:00.000000000 ETHUSD 163.32   163.32   163.24   163.25   51.01684   5.685048 163.25   163...
    2020.02.02 2020.02.02D17:49:00.000000000 BTCUSD 9438.97  9439.99  9438.51  9438.51  0.00803406 5.716079 9438.51  9439..
    2020.03.14 2020.03.14D10:39:00.000000000 ETHUSD 129.07   129.07   129.07   129.07   0          5.796661 129.07   129...
    2020.01.17 2020.01.17D07:27:00.000000000 ETHUSD 169.96   169.96   169.96   169.96   0          5.868941 169.96   169...
    2020.01.27 2020.01.27D12:49:00.000000000 ETHUSD 169.07   169.07   169.07   169.07   0.005438   5.896844 169.07   169...
    2020.07.27 2020.07.27D12:33:00.000000000 BTCUSD 10300    10301.45 10300    10301.45 0.00227084 5.908368 10301.45 1029..
    2020.01.17 2020.01.17D07:27:00.000000000 BTCUSD 8898.73  8905.63  8898.73  8898.73  0.01085345 5.935242 8898.73  8898..
    2020.04.04 2020.04.04D19:39:00.000000000 BTCUSD 6761.54  6765.15  6761.54  6765.15  0.00404    5.947631 6765.15  6762..
    2020.05.25 2020.05.25D11:32:00.000000000 BTCUSD 8732.04  8736.19  8728.84  8730.83  1.38589    5.956008 8730.83  8730..

This example demonstrates how to filter the data based on specific symbols (BTCUSD, ETHUSD) and search for patterns within each symbol. By iterating over the date-symbol pairs and applying a time series search, we refine the dataset. The filtered results highlight the top k matches based on the minimum distance, which provides an efficient way to focus on the most relevant data for further analysis.


## Conclusion

This tutorial demonstrated how to:
- Efficiently load and process data from CSV files into a time series database.
- Perform advanced time series searches to identify specified patterns of interest, focusing on cryptocurrency price movements.
- Optimize and conduct TSS operations across partitioned databases to ensure scalable and high-performance querying.